buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.1.11'
  }
}
plugins {
  id 'io.codearte.nexus-staging' version '0.9.0'
  id 'net.researchgate.release' version '2.4.0'
}

nexusStaging {
  username = ossrhUsername
  password = ossrhPassword
  stagingProfileId = '6c61426e6529d'
}

allprojects  {
  apply plugin: 'maven'

  group = 'com.yubico'
  version = '0.16.1-SNAPSHOT'
}

subprojects {
  apply plugin: 'java'
  sourceCompatibility = 1.6
  targetCompatibility = 1.6

  task wrapper(type: Wrapper) {
    gradleVersion = '4.1'
  }

  tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
  }

  task packageSources(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
  }
  artifacts.archives packageSources

  task packageJavadoc(type: Jar) {
    classifier = 'javadoc'
    inputs.files javadoc.outputs
    from javadoc.destinationDir
  }
  artifacts.archives packageJavadoc

  repositories {
    mavenLocal()

    maven { url "http://repo.maven.apache.org/maven2" }
  }


  dependencies {

    testCompile(
      [group: 'junit', name: 'junit', version:'4.12'],
      [group: 'org.mockito', name: 'mockito-all', version:'2.0.2-beta'],
    )

  }

  uploadArchives {
    repositories {
      mavenDeployer {
        repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
          authentication(userName: ossrhUsername, password: ossrhPassword)
        }

        snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
          authentication(userName: ossrhUsername, password: ossrhPassword)
        }
      }
    }
  }

}
