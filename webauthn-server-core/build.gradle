import java.nio.file.Paths

description = 'WebAuthn core'

apply plugin: 'scala'

configurations {
  scalaRepl { extendsFrom testRuntime }
}

dependencies {

  compile(
    project(':u2flib-server-attestation'),
    project(':u2flib-server-core'),
    [group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version:'1.54'],
    [group: 'com.google.guava', name: 'guava', version:'19.0'],
    [group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version:'2.6.5'],
    [group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-cbor', version:'2.6.5'],
    'org.scala-lang:scala-library:2.12.3'
  )

  testCompile(
    'org.scalatest:scalatest_2.12:3.0.4',
    'commons-io:commons-io:2.5',
  )

  scalaRepl(
    'org.scala-lang:scala-compiler:2.12.3',
    jar.outputs.files,
  )

}

def scalaReplDistDir = file(Paths.get(project.distsDir.path, 'scala-repl'))
task scalaReplClasspath(type: Sync) {
  from configurations.scalaRepl
  destinationDir = file(Paths.get(scalaReplDistDir.path, 'lib'))
}

task makeScalaReplScripts(type: CreateStartScripts) {
  inputs.files tasks.scalaReplClasspath.outputs

  applicationName = 'scala-repl'
  classpath = configurations.scalaRepl
  mainClassName = "scala.tools.nsc.MainGenericRunner"
  outputDir = file(Paths.get(scalaReplDistDir.path, 'bin'))
}
